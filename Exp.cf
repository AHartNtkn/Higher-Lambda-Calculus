entrypoints Module, Exp ;

comment "--" ;
comment "{-" "-}" ;

layout "where", "let" ;
layout stop "in" ;
-- Do not use layout toplevel as it makes pExp fail!

Module.   Module ::= "module" AIdent "where" "{" [Imp] [Decl] "}" ;

Import.   Imp ::= "import" AIdent ;
separator Imp ";" ;

DeclDef.    Decl ::= AIdent ":" Exp "=" ExpWhere ;
separator   Decl ";" ;

Where.    ExpWhere ::= Exp "where" "{" [Decl] "}" ;
NoWhere.  ExpWhere ::= Exp ;

SLet.          Exp  ::= "let" "{" [Decl] "}" "in" Exp ;
SDec.          Exp1 ::= "(" Exp1 "::" Exp1 ")" ;
SApp.          Exp1 ::= Exp1 Exp2 ;
SLam.          Exp2 ::= [PTele] Exp1 ;
SVar.          Exp3 ::= AIdent ;
SU.            Exp3 ::= "U[" Lv "]" ;
SHole.         Exp3 ::= HoleIdent ;
coercions Exp 3 ;

PTele.    PTele ::= "(" Exp ":" Exp ")" ;
terminator nonempty PTele "" ;

token AIdent ('_')|(letter)(letter|digit|'\''|'_')*|('!')(digit)* ;
separator AIdent "" ;

token Lv (digit)+ ;
separator Lv "" ;

token HoleIdent '?' ;
